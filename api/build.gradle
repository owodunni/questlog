import org.openapitools.generator.gradle.plugin.tasks.GenerateTask

plugins {
  id "questlog.java-conventions"
  id "distribution"
  id "org.openapi.generator" version "5.1.0"
  id 'com.palantir.docker-run' version "0.26.0"
}

version "0.1.0"

def apiFile = "openapi.yaml"
def apiSpec = "$projectDir/$apiFile"
def apiName = "${project.group}.${project.name}"
def artifactDir = "$buildDir/api"

openApiValidate {
  inputSpec = apiSpec
  recommend = true
}

task generatePythonFlask(type: GenerateTask) {
  dependsOn "openApiValidate"
  generatorName = "python-flask"
  inputSpec = apiSpec
  packageName = "questlog"
  outputDir = "$artifactDir/$name"
}

distTar.dependsOn generatePythonFlask
distZip.dependsOn generatePythonFlask

distributions {
  main {
    distributionBaseName = apiName
    contents {
      from file(generatePythonFlask.outputDir)
    }
  }
}

artifacts {
  "default" distTar
}

dockerRun {
  name 'openapi'
  image 'swaggerapi/swagger-editor'
  volumes "$projectDir": '/tmp'
  ports '8070:8080'
  daemonize true
  env 'SWAGGER_FILE':"/tmp/$apiFile"
}
