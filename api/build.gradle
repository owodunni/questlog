import org.openapitools.generator.gradle.plugin.tasks.GenerateTask

plugins {
  id "questlog.java-conventions"
  id "distribution"
  id "org.openapi.generator" version "5.1.0"
}

version "0.1.0"

def apiSpec = "$projectDir/openapi.yaml"
def packageName = "io.questlog"
def artifactDir = "$buildDir/api"

openApiValidate {
  inputSpec = apiSpec
  recommend = true
}

task generatePythonFlask(type: GenerateTask) {
  dependsOn "openApiValidate"
  generatorName = "python-flask"
  inputSpec = apiSpec
  outputDir = "$artifactDir/$name"
  validateSpec = true
  apiPackage = "${packageName}.api"
  invokerPackage = "${packageName}.invoker"
  modelPackage = "${packageName}.model"
}

distTar.dependsOn generatePythonFlask
distZip.dependsOn generatePythonFlask

distributions {
  main {
    distributionBaseName = "${project.group}.${project.name}"
    contents {
      from file(generatePythonFlask.outputDir)
    }
  }
}

artifacts {
  "default" distTar
}

class KonsoleTask extends DefaultTask {
  @Input
  String command

  @TaskAction
  void launchKonsole() {
    ext.process = new ProcessBuilder()
        .directory(project.projectDir)
        .command('konsole', '--noclose', '-e', command)
        .start()
  }
}

task develop(type: KonsoleTask) {
  command = "docker run -p 8080:8080 -v $projectDir:/tmp -e SWAGGER_FILE=$apiSpec swaggerapi/swagger-editor"
}
