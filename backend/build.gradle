plugins {
  id "questlog.java-conventions"
  id "com.diffplug.spotless"
}

spotless {
  ratchetFrom 'origin/main'

  python {
    target fileTree(dir: projectDir, include: '**/*.py')
    black("20.8b1").pathToExe("${rootProject.ext.pythonDir}/black")
  }
}

version "0.1.0"

def depsDir = "$buildDir/extractedDpendencies"
def testResultDir = "$buildDir/outputs/tests"
def srcDir = "$projectDir/questlog"
def generatedDir = "$srcDir/generated"

configurations {
  api
}

dependencies {
  api project(path: ":api")
}


task pip(type: PipTask) {
  workingDir projectDir
  setup([
    "requirements.txt",
    "test-requirements.txt"
  ], rootProject.ext.pythonDir)
}

task initialize(){
  dependsOn pip
}

task extractDependencies(type: Copy) {
  dependsOn configurations.api
  from {
    configurations.api.collect {
      tarTree(it)
    }
  }
  into depsDir
}

task extractModels(type: Copy) {
  dependsOn extractDependencies
  from "$depsDir/io.questlog.api-0.1.0/questlog/generated"
  into "$generatedDir"
}

task extractApi(type: Copy) {
  dependsOn extractDependencies
  from "$depsDir/io.questlog.api-0.1.0/questlog/openapi"
  into "$generatedDir/api"
}

task assembleBackend(){
  dependsOn extractApi, extractModels
}

def testArgs = [
  "${rootProject.ext.pythonDir}/pytest",
  "-v",
  "--pyargs",
  "$srcDir/test",
  "--cov-report",
  "html:$testResultDir/cov/html",
  "--cov-report",
  "xml:$testResultDir/cov/coverage.xml",
  "--junitxml=$testResultDir/surefire-reports/TEST-report.xml",
  "--cov=$srcDir"
]

task testUnit(type: Exec){
  dependsOn assembleBackend
  workingDir projectDir

  commandLine(testArgs)
}

task test(type: Exec) {
  dependsOn assembleBackend
  workingDir projectDir

  def testArgsIntegration = testArgs + "--integration"

  commandLine(testArgsIntegration)
}

build.dependsOn test
assemble.dependsOn assembleBackend
