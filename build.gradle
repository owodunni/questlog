plugins {
  id "com.diffplug.spotless" version "5.12.4"
}

repositories {
  mavenCentral()
}

spotless {
  ratchetFrom 'origin/main'

  format 'misc', {
    target fileTree(dir: projectDir, includes: [
      '**/*.md',
      '**/.gitignore',
      '**/*.txt'
    ])

    trimTrailingWhitespace()
    indentWithSpaces()
    endWithNewline()
  }
  groovyGradle {
    target fileTree(dir: projectDir, include: '**/*.gradle')
    greclipse().configFile('.groovy.eclipseformat.prefs')
  }
}

spotlessCheck.dependsOn("app:styleCheck")
spotlessApply.dependsOn("app:styleFix")


task venv(type: Exec) {
  commandLine "python3", "-m", "venv", ".venv"
}

task pip(type: Exec) {
  workingDir projectDir
  def reqs = [
    file("project-requirements.txt"),
  ]

  inputs.files reqs

  def args = ["pip", "install"]

  reqs.each { req ->
    args += ["-r", req]
  }
  commandLine args
}

task preCommit(type: Exec) {
  dependsOn pip
  commandLine "pre-commit",
      "install",
      "--hook-type",
      "commit-msg"
}

task czCheck(type: Exec) {
  commandLine "cz", "check", "--rev-range", "HEAD~..HEAD"
}

task initialize(){
  dependsOn pip, preCommit
}

task lint(){
  dependsOn czCheck, spotlessCheck
}

assemble.dependsOn lint
