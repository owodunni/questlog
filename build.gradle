plugins {
  id "questlog.java-conventions"
  id "com.diffplug.spotless" version "5.12.4"
}

spotless {
  ratchetFrom 'origin/main'

  format 'misc', {
    target fileTree(dir: projectDir, includes: [
      '**/*.md',
      '**/.gitignore',
      '**/*.txt'
    ])

    trimTrailingWhitespace()
    indentWithSpaces()
    endWithNewline()
  }
  groovyGradle {
    target fileTree(dir: projectDir, include: '**/*.gradle')
    greclipse().configFile('.groovy.eclipseformat.prefs')
  }
  kotlin {
    target fileTree(dir: projectDir, include: '**/*.kt')
    ktlint()
  }
  kotlinGradle {
    target fileTree(dir: projectDir, include: '**/*.gradle.kts')
    ktlint()
  }
}

spotlessCheck.dependsOn("app:styleCheck")
spotlessApply.dependsOn("app:styleFix")


task venv(type: Exec) {
  commandLine "python3", "-m", "venv", ".venv"
}

task pip(type: PipTask) {
  workingDir projectDir
  setPipFiles(["project-requirements.txt"])
}

task preCommit(type: Exec) {
  dependsOn pip
  commandLine "pre-commit",
      "install",
      "--hook-type",
      "commit-msg"
}

task czCheck(type: Exec) {
  commandLine "cz", "check", "--rev-range", "HEAD~..HEAD"
}

task initialize(){
  dependsOn pip, preCommit
}

task lint(){
  dependsOn czCheck, spotlessCheck
}

assemble.dependsOn lint
